---
title: ä»0åˆ°1æ„å»ºè®¡ç®—æœº(10/12)--ç®€å•çš„æ“ä½œç³»ç»Ÿ
tags: è®¡ç®—æœºåŸºç¡€ ä»0åˆ°1æ„å»ºè®¡ç®—æœº
---
ä¸Šä¸€ç¯‡æˆ‘ä»¬å®Œæˆäº†Jackçš„ç¼–è¯‘å™¨ï¼Œè¿™å°±æ„å‘³ç€æˆ‘ä»¬å¯ä»¥éšå¿ƒæ‰€æ¬²åœ°åœ¨Hackä¸Šç¼–å†™Jackç¨‹åºäº†ã€‚ä½†æ˜¯ä»¥Jackç›®å‰çš„èƒ½åŠ›ï¼Œå³ä½¿æ˜¯æƒ³å®Œæˆä¸€äº›ç®€å•çš„æ“ä½œä¹Ÿè¦ç¼–å†™éå¸¸å¤æ‚çš„ä»£ç ï¼Œä¾‹å¦‚æˆ‘ä»¬æƒ³è¦åœ¨å±å¹•ä¸Šè¾“å‡ºâ€œHello Workd!â€ï¼Œå°±å¿…é¡»åœ¨å±å¹•ä¸Šç‰¹å®šä½ç½®ç”»ä¸Šå‡ ç™¾ä¸ªåƒç´ ç‚¹ã€‚æ‰€ä»¥æˆ‘ä»¬éœ€è¦å®ç°ä¸€ä¸ªç®€å•çš„æ“ä½œç³»ç»Ÿæ¥è®©äº‹æƒ…å˜å¾—æ›´ç®€å•ä¸€äº›ã€‚

æ“ä½œç³»ç»Ÿå°±æ˜¯ç”¨æ¥è¡”æ¥è®¡ç®—æœºçš„ç¡¬ä»¶ç³»ç»Ÿå’Œè½¯ä»¶ç³»ç»Ÿçš„ï¼Œä»¥ä½¿å¾—è®¡ç®—æœºå¯¹ç¨‹åºå‘˜å’Œç”¨æˆ·è€Œè¨€æ›´å®¹æ˜“ä½¿ç”¨ã€‚æœ¬ç¯‡çš„æ“ä½œç³»ç»ŸåŒ…å«äº†ä¸€ä¸ªæœ€å°è§„æ¨¡çš„æ“ä½œç³»ç»Ÿï¼ˆå…¶å®å°±æ˜¯ä¸€ä¸ªJackæ ‡å‡†åº“çš„é›†åˆï¼‰ï¼Œä¸»è¦åŒ…æ‹¬ï¼šæ•°å­¦è¿ç®—ã€å­—ç¬¦ä¸²æ“ä½œã€å†…å­˜ç®¡ç†ã€æ–‡æœ¬å’Œå›¾å½¢è¾“å‡ºåˆ°å±å¹•ã€é”®ç›˜è¾“å…¥å¤„ç†ç­‰åŠŸèƒ½ï¼Œä¸åŒ…æ‹¬ï¼šè¿›ç¨‹ç®¡ç†ã€ç£ç›˜ç®¡ç†ã€é€šä¿¡ç­‰åŠŸèƒ½ã€‚

### æ•°å­¦æ“ä½œ

ç›®å‰Hackåœ¨ç¡¬ä»¶ä¸Šåªæ”¯æŒåŠ æ³•å’Œå‡æ³•ï¼Œå› æ­¤æˆ‘ä»¬éœ€è¦åœ¨è½¯ä»¶å±‚é¢å®Œå–„Hackçš„æ•°å­¦æ“ä½œï¼ŒåŒ…æ‹¬ï¼šä¹˜æ³•ã€é™¤æ³•ã€ç»å¯¹å€¼ã€æœ€å¤§å€¼ã€æœ€å°å€¼ã€å¼€å¹³æ–¹æ“ä½œã€‚

**æ•ˆç‡ç¬¬ä¸€**ï¼Œåœ¨åŠ å‡æ³•çš„åŸºç¡€ä¸Šå®ç°è¿™äº›æ•°å­¦æ“ä½œçš„æ–¹å¼å¯èƒ½æœ‰å¾ˆå¤šï¼Œåœ¨è¿™é‡Œæˆ‘ä»¬æœ€åº”è¯¥å…³æ³¨çš„æ˜¯æ‰§è¡Œçš„æ•ˆç‡ï¼Œå› ä¸ºæ•°å­¦æ“ä½œæ˜¯è®¡ç®—æœºå·¥ä½œæ—¶æœ€å¸¸åšçš„åŸºæœ¬æ“ä½œä¹‹ä¸€ï¼Œæœ€å¤§ç¨‹åº¦çš„ä¼˜åŒ–æ•°å­¦æ“ä½œæ˜¯ä¼˜åŒ–è®¡ç®—æœºæ€§èƒ½çš„é‡è¦ä¸»é¢˜ã€‚ä¾‹å¦‚CPUä¼šä¸“é—¨ä¸ºæµ®ç‚¹æ•°è®¾è®¡æµ®ç‚¹æ•°è®¡ç®—å•å…ƒï¼Œæ›´æ“…é•¿æ•°å­¦è¿ç®—çš„GPUä¹Ÿè¢«è®¾è®¡å‡ºæ¥ç”¨äºè¾…åŠ©è®¡ç®—æœºçš„æ•°å­¦è¿ç®—ã€‚

#### ä¹˜æ³•

å¯¹äºä¹˜æ³• sum=x\*yï¼Œæœ€ç®€å•çš„æ–¹æ³•æ˜¯æŠŠ sum=sum+x é‡å¤è¿ç®— y æ¬¡ï¼Œç®—æ³•çš„æ—¶é—´å¤æ‚åº¦ä¸º O(n)ã€‚å¦ä¸€ç§æ–¹å¼æ˜¯æ¨¡æ‹Ÿå°å­¦è¿ç®—ä¹˜æ³•çš„ç«–å¼æ–¹å¼ï¼šç”¨ shiftedx å»ä¹˜ y çš„æ¯ä¸€ä½ï¼Œå¦‚æœ y çš„è¿™ä½ä¸º1ï¼Œåˆ™æŠŠç»“æœåŠ åˆ° sum ä¸­ï¼Œå¦‚æœä¸º0åˆ™èˆå¼ƒï¼›å¤„ç†å®Œ y çš„å½“å‰ä½åï¼Œå¤„ç† y çš„ä¸‹ä¸€ä½ï¼Œæ­¤æ—¶éœ€è¦æ“ä½œ shiftedx=shiftedx\*2ï¼ˆæ­¤æ“ä½œå¯ä»¥é€šè¿‡åŠ æ³•å®ç°ï¼Œæˆ–è€…é€šè¿‡ç§»ä½å®ç°ï¼Œç§»ä½æ•ˆç‡æ›´é«˜ï¼Œè™½ç„¶Hackä¸æ”¯æŒç§»ä½æ“ä½œï¼‰ã€‚ç¬¬äºŒç§æ–¹å¼æ•ˆç‡æ›´é«˜ï¼Œç®—æ³•çš„æ—¶é—´å¤æ‚åº¦ä¸º O(log2(n))ã€‚

<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcq3caoxlhj30ra09mq2w.jpg" width="500"></center>
<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcq2u7z2gtj31220bijrp.jpg" width="600"></center>

å®ç°ï¼š
```java
static int powers_of_two;

function void init() {
    let powers_of_two = Array.new(16);
    let powers_of_two[0] = 1;
    let powers_of_two[1] = 2;
    let powers_of_two[2] = 4;
    let powers_of_two[3] = 8;
    let powers_of_two[4] = 16;
    let powers_of_two[5] = 32;
    let powers_of_two[6] = 64;
    let powers_of_two[7] = 128;
    let powers_of_two[8] = 256;
    let powers_of_two[9] = 512;
    let powers_of_two[10] = 1024;
    let powers_of_two[11] = 2048;
    let powers_of_two[12] = 4096;
    let powers_of_two[13] = 8192;
    let powers_of_two[14] = 16384;
    let powers_of_two[15] = 16384+16384;
    return;
}

// åˆ¤æ–­xçš„ç¬¬nä½æ˜¯å¦ä¸º1
function boolean bit(int x, int n) {
    return ~((x & powers_of_two[n]) = 0);
}

function int multiply(int x, int y) {
    var int sum, shiftedX;
    var int j;
    
    let sum = 0;
    let shiftedX = x;
    let j = 0;
    while( j < 16 ) {   // 16-bit numbers
        if(Math.bit(y, j)) {
            let sum = sum + shiftedX;
        }
        let shiftedX = shiftedX + shiftedX;
        let j = j + 1;
    }
    return sum;
}
```

#### é™¤æ³•

å¯¹äºé™¤æ³• d=x/yï¼Œå®¹æ˜“æƒ³åˆ°çš„æ–¹æ³•æ˜¯é‡å¤è®¡ç®— x=x-yï¼Œæ¯è®¡ç®—ä¸€æ¬¡å d=d+1ï¼Œç›´è‡³ x-y<0 ä¸ºæ­¢ã€‚åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œæœ€åçš„æƒ…å†µæ˜¯ y=1 çš„æƒ…å†µï¼Œæ­¤æ—¶çš„æ­¥é•¿æœ€çŸ­ï¼Œéœ€è¦é‡å¤è®¡ç®—xæ¬¡ï¼Œæ—¶é—´å¤æ‚åº¦ä¸º O(x)ã€‚æˆ‘ä»¬å¯ä»¥è¿›ä¸€æ­¥ä¼˜åŒ–ï¼Œå¢åŠ æ­¥é•¿ï¼Œæ¯æ¬¡ä¸æ˜¯å‡å»yï¼Œè€Œæ˜¯å‡å»2yï¼Œè€Œä¸”æ˜¯é€’å½’çš„è¿›è¡Œä¸‹å»ï¼Œç›´è‡³ x<y æ—¶è¿”å›ï¼Œæ­¤æ—¶æœ€åçš„æ—¶é—´å¤æ‚åº¦ä¸º O(log2(n))ã€‚

<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcq7h7f8ezj311m0bk3yt.jpg" width="600"></center>

```java
function int divide(int x, int y) {
    var int neg_x, neg_y;
    var int q;
    var int result;
    
    let neg_x = x < 0;
    let neg_y = y < 0;
    let x = Math.abs(x);
    let y = Math.abs(y);

    if( y > x ) {
        return 0;
    }
    let q = Math.divide(x, y+y);
    if( x-(2*q*y) < y ) {
        let result = q+q;
    }
    else {
        let result = q+q+1;
    }
    
    if( neg_x = neg_y ) {
        return result;
    }
    else {
        return -result;
    }
}
```

#### å¼€å¹³æ–¹

è¿™ä¸ªå¼€å¹³æ–¹å‡½æ•°è¿”å›å…¥å‚ x å¼€å¹³æ–¹åç»“æœçš„æ•´æ•°éƒ¨åˆ†ã€‚ç®—æ³•å¯ä»¥ä½¿ç”¨äºŒåˆ†æŸ¥æ‰¾çš„æ–¹å¼ï¼Œæ‰¾åˆ° $y^2\leq x<(y+1)^2$ å³å¯ã€‚è¯¥ç®—æ³•çš„æ—¶é—´å¤æ‚åº¦ä¸ºO(log2(n))ï¼Œä½†ç”±äºæˆ‘ä»¬è®¡ç®—çš„ x å¤§å°æœ‰ä¸Šé™ï¼ˆ$2^{16}-1$ï¼‰ï¼Œæ‰€ä»¥çœŸå®æ‰§è¡Œæ—¶å¾ªç¯çš„æ¬¡æ•°æ˜¯å›ºå®šçš„ n/2 (n=16)ï¼Œå¤æ‚åº¦æ˜¯O(n/2)ã€‚

```java
ä¼ªä»£ç ï¼š
y = 0
for j = n/2 - 1 ... 1, 0 do
    if (y+2^j)^2 < x then y = 2^j
return y

function int sqrt(int x) {
    var int j, y;
    var int approx;
    var int approx_squared;
    
    let y = 0;
    let j = 7;      // = #bits / 2 - 1
    while( ~(j < 0) ) {
        let approx = y + powers_of_two[j];
        let approx_squared = approx * approx;
        if( ~(approx_squared > x) & (approx_squared > 0) ) {    // in case of overflow
            let y = approx;
        }
        let j = j - 1;
    }
    return y;
}
```

#### ç»å¯¹å€¼ã€æœ€å¤§å€¼ã€æœ€å°å€¼ã€å–æ¨¡

è¿™äº›è¿ç®—å°±æ¯”è¾ƒç®€å•äº†

```java
function int max(int a, int b) {
    if( a > b ) {
        return a;
    }
    else {
        return b;
    }
}

function int min(int a, int b) {
    if( a < b ) {
        return a;
    }
    else {
        return b;
    }
}

function int abs(int x) {
    if( x < 0 ) {
        let x = -x;
    }
    return x;
}

function int mod(int x, int y) {
    var int q;
    
    let q = Math.divide(x, y);
    return x - (q*y);
}
```

### å­—ç¬¦ä¸²

å­—ç¬¦ä¸²çš„é€»è¾‘æ¯”è¾ƒç®€å•ã€‚åœ¨Jackä¸­ï¼Œä¸€ä¸ªå­—ç¬¦ä¸²æ˜¯ä¸€ä¸ªæ•°ç»„ï¼Œå­˜å‚¨å†…å®¹ä¸€ä¸²asciiç¼–ç ï¼ˆä¸”ä¸éœ€è¦ä»¥\0ç»“å°¾ï¼‰ã€‚æˆ‘ä»¬ç”¨Stringç±»æ¥å®ç°ï¼Œå…¶ä¸­æœ€æ ¸å¿ƒçš„ä¸¤ä¸ªæ–¹æ³•æ˜¯æ„é€ å‡½æ•° new(int length) å’Œ appendChar(char c)ï¼Œè¿™é‡Œæˆ‘ä»¬åªæ”¯æŒä¸€ä¸ªä¸€ä¸ªåœ°è¿½åŠ å­—ç¬¦ï¼Œä¸æ”¯æŒç›´æ¥ä¼ å…¥ä¸€ä¸ªå­—ç¬¦ä¸²ğŸ˜‚ã€‚é™„ä¸Š[asciiç¼–ç è¡¨ï¼š](https://zh.wikipedia.org/wiki/ASCII)

```java
class String {
    field Array buffer;
    field int buffer_len;
    field int str_len;
    
    constructor String new(int maxLength) {
        if( maxLength = 0 ) {
            let maxLength = 1;
        }
        let buffer = Array.new(maxLength);
        let buffer_len = maxLength;
        let str_len = 0;
        return this;
    }

    method void dispose() {
        do Array.dispose(buffer);
        return;
    }

    method int length() {
        return str_len;
    }

    method char charAt(int j) {
        return buffer[j];
    }

    method void setCharAt(int j, char c) {
        let buffer[j] = c;
        return;
    }

     *  Returns this string as the return value. */
    method String appendChar(char c) {
        if( str_len < buffer_len ) {
            let buffer[str_len] = c;
            let str_len = str_len + 1;
        }
        return this;
    }

    method void eraseLastChar() {
        if( str_len > 0 ) {
            let str_len = str_len - 1;
        }
        return;
    }

    /** è¿”å›å­—ç¬¦ä¸²ä»£è¡¨çš„æ•´æ•°å€¼ */
    method int intValue() {
        var int int_val;
        var int i;
        var boolean neg;
        
        let int_val = 0;
        
        if( (str_len > 0) & (buffer[0] = 45) ) {      // '-'
            let neg = true;
            let i = 1;
        }
        else {
            let neg = false;
            let i = 0;
        }
        
        while( (i < str_len) & String.is_digit(buffer[i]) ) {
            let int_val = (int_val * 10) + String.digit_val(buffer[i]);
            let i = i + 1;
        }
        
        if( neg ) {
            return -int_val;
        }
        else {
            return int_val;
        }
    }
    
    function boolean is_digit(char c) {
        return ~(c < 48) & ~(c > 57);
    }
    
    function int digit_val(char c) {
        return c - 48;
    }
    
    function char digit_char(int i) {
        return i + 48;
    }

    /** ä»¥å­—ç¬¦ä¸²çš„å½¢å¼ä¿å­˜æ•´æ•° */
    method void setInt(int number) {
        let str_len = 0;    // Clear string
        
        if( number < 0 ) {
            let number = -number;
            do appendChar(45);      // leading '-'
        }
        
        do do_set_int(number);
        return;
    }

    method void do_set_int(int number) {
        var int q;
        var int mod;
        var char c;
        
        let q = number / 10;
        let mod = number - (q*10);
        let c = String.digit_char(mod);
        
        if( number < 10 ) {
            do appendChar(c);
        }
        else {
            do do_set_int(q);
            do appendChar(c);
        }
        return;
    }
    
    function char newLine() {
        return 128;
    }

    function char backSpace() {
        return 129;
    }

    /** åŒå¼•å· */
    function char doubleQuote() {
        return 34;
    }
}
```

### å†…å­˜ç®¡ç†

ä½¿ç”¨é«˜çº§è¯­è¨€ï¼Œæœ€å¤§çš„æ–¹ä¾¿ä¹‹ä¸€å°±æ˜¯å¼€å‘è€…å‡ ä¹ä¸ç”¨å…³æ³¨å†…å­˜ç®¡ç†çš„ç»†èŠ‚ï¼Œæ‰€æœ‰å¤æ‚çç¢çš„å·¥ä½œéƒ½åˆæ“ä½œç³»ç»Ÿï¼Œç¼–è¯‘å™¨ï¼Œè™šæ‹Ÿæœºï¼Œæ±‡ç¼–å™¨ç­‰åº•å±‚æ¥è´Ÿè´£å®ç°ã€‚ä¸åŒå˜é‡ç±»å‹çš„å†…å­˜åœ¨ç¨‹åºå£°æ˜å‘¨æœŸä¸­çš„ä¸åŒæ—¶åˆ»è¢«åˆ†é…ï¼Œä¾‹å¦‚ï¼Œé™æ€å˜é‡åœ¨ç¼–è¯‘æœŸç”±ç¼–è¯‘å™¨ä¸ºå…¶åˆ†é…å†…å­˜ï¼›å±€éƒ¨å˜é‡åˆ™åœ¨æ¯ä¸ªå‡½æ•°è¿è¡Œçš„æ—¶å€™åˆ†é…åœ¨æ ˆå†…å­˜ä¸Šï¼›å…¶ä»–å†…å­˜åˆ™æ˜¯åœ¨ç¨‹åºè¿è¡Œæ—¶è¢«åŠ¨æ€åœ°åˆ†é…åœ¨å †å†…å­˜ä¸Šï¼Œè¿™äº›å†…å­˜å°±éœ€è¦å€ŸåŠ©æ“ä½œç³»ç»Ÿæ¥ç®¡ç†äº†ã€‚æ“ä½œç³»ç»Ÿå¯èƒ½ä½¿ç”¨ä¸åŒçš„æ–¹å¼æ¥ç®¡ç†å†…å­˜çš„åˆ†é…å’Œå›æ”¶ï¼Œé€šå¸¸éƒ½æ˜¯ç”¨alloc()å’ŒdeAlloc()è¿™ä¸¤ä¸ªå‡½æ•°ä¸­å®ç°çš„ã€‚

#### æœ€ç®€å•çš„å†…å­˜ç®¡ç†ç®—æ³•

ä½¿ç”¨ä¸€ä¸ªå•ä¸€çš„æŒ‡é’ˆ free æŒ‡å‘è¿˜æœªä½¿ç”¨è¿‡çš„å†…å­˜çš„åŸºåœ°å€ã€‚è¿™ç§æ–¹å¼åªæ˜¯ç®€å•çš„å‘å‰åˆ†é…å†…å­˜ï¼Œä¸è´Ÿè´£å›æ”¶å†…å­˜ã€‚

```java
// ä¼ªä»£ç 
initialize free = heapBase

// åˆ†é…ä¸€å—å¤§å°ä¸º size çš„å†…å­˜
alloc(size)
  pointer = free
  free = free + size 
  return pointer

// å›æ”¶objectçš„å†…å­˜
deAlloc(object)
do nothing        // ä¸åšå†…å­˜å›æ”¶å¤„ç†
```

#### ç®—æ³•æ”¹è¿›

ç»å…¸çš„ç®—æ³•æ˜¯å¯ä»¥ä½¿ç”¨é“¾è¡¨æ¥ç®¡ç†å†…å­˜ã€‚è¯¥ç®—æ³•ç®¡ç†ä¸€ä¸ªfreeListé“¾è¡¨ï¼Œé“¾è¡¨çš„æ¯ä¸€ä¸ªèŠ‚ç‚¹éƒ½æ˜¯ä¸€ä¸ªå†…å­˜æ®µï¼Œè°ƒç”¨è€…ç”³è¯·åˆ†é…å†…å­˜æ—¶ï¼Œæ“ä½œç³»ç»Ÿä»å¯ç”¨å†…å­˜æ®µä¸­æŒ‘é€‰ä¸€ä¸ªåˆé€‚çš„å†…å­˜æ®µï¼ˆå†…å­˜æ®µå†…å­˜å¤§äºç”³è¯·çš„å†…å­˜ï¼‰ï¼Œå°†å…¶ï¼ˆå†…å­˜æ®µçš„å…¨éƒ¨å†…å­˜æˆ–éƒ¨åˆ†å†…å­˜ï¼‰è¿”å›ç»™è°ƒç”¨è€…ï¼Œç„¶åæ›´æ–°é“¾è¡¨ã€‚è°ƒç”¨è€…ç”³è¯·å›æ”¶å†…å­˜æ—¶ï¼Œæ“ä½œç³»ç»ŸæŠŠéœ€è¦å›æ”¶çš„è¿™æ®µå†…å­˜åŠ å…¥freeListé“¾è¡¨ã€‚

æ¯ä¸ªå†…å­˜æ®µçš„ç¬¬ä¸€ä¸ªä½ç½®ç”¨äºä¿å­˜è¯¥å†…å­˜æ®µçš„é•¿åº¦ï¼Œç¬¬äºŒä¸ªä½ç½®ç”¨äºæŒ‡å‘ Next å†…å­˜æ®µã€‚å½“æ“ä½œç³»ç»ŸæŸ¥æ‰¾åˆé€‚çš„å†…å­˜æ®µæ—¶å¯ä»¥ä½¿ç”¨**æœ€ä¼˜é€‚åº”ç®—æ³•**ï¼Œå³æŸ¥æ‰¾æ•´ä¸ªé“¾è¡¨ä¸­å¤§å°æœ€åŒ¹é…å˜é‡çš„å†…å­˜æ®µï¼›æˆ–è€…**æœ€å…ˆé€‚åº”ç®—æ³•**ï¼Œå³æ‰¾åˆ°ç¬¬ä¸€ä¸ªæ»¡è¶³å˜é‡éœ€æ±‚çš„å†…å­˜æ®µã€‚

æˆ‘ä»¬ä»¥ä¸‹å›¾ä¸ºä¾‹ï¼Œå½“å‰çš„freeListçŠ¶æ€å¦‚å·¦å›¾æ‰€ç¤ºï¼Œæœ‰ä¸‰å—å†…å­˜ï¼Œå…¶å†…å­˜æ®µå¤§å°åˆ†åˆ«æ˜¯4ã€9ã€5ï¼ˆå¯ç”¨å¤§å°æ˜¯2ã€7ã€3ï¼‰ã€‚å½“è°ƒç”¨è€…æ‰§è¡Œalloc(5)æ—¶ï¼Œä»£è¡¨è°ƒç”¨è€…éœ€è¦å¤§å°ä¸º5çš„å†…å­˜ï¼Œæ­¤æ—¶ä¸‰å—å†…å­˜ä¸­åªæœ‰å¤§å°ä¸º9çš„å†…å­˜å—æ»¡è¶³éœ€æ±‚ï¼Œæˆ‘ä»¬ä¸­å–å‡ºå6ä½å†…å­˜è¿”å›ç»™è°ƒç”¨è€…ï¼ˆå¤šçš„ä¸€ä½ç”¨æ¥æ ‡è¯†è¯¥å˜é‡å ç”¨å†…å­˜å¤§å°ï¼Œåç»­ç”¨äºå†…å­˜å›æ”¶æ—¶ä½¿ç”¨ï¼‰ï¼Œç„¶åå¤§å°ä¸º9çš„å†…å­˜å—å°±è¿˜å‰©3äº†ã€‚

<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcuxvnsui0j31dq0lq0t3.jpg" width="700"></center>

```java
// ä¼ªä»£ç 
Initialize:
	freeList = heap
	freeList.length = heapLength
	freeList.next = null

// åˆ†é…å†…å­˜
alloc(size):
	åˆ©ç”¨â€œæœ€ä¼˜é€‚åº”ç®—æ³•â€æˆ–è€…â€œæœ€å…ˆé€‚åº”ç®—æ³•â€æ‰¾åˆ°åˆé€‚çš„å†…å­˜æ®µï¼ˆæ»¡è¶³æ¡ä»¶ segement.length > sizeï¼‰
	if æ‰¾ä¸åˆ°
	  è¿”å› fail æˆ–è€…å°è¯•æ•´åˆå†…å­˜ç¢ç‰‡
	else
	  block = ä»å†…å­˜æ®µä¸­å–å‡ºä¸€æ®µå†…å­˜ï¼Œå¤§å°ä¸º size + 1ï¼ˆå¦‚æœå–å‡ºåï¼Œè¯¥å†…å­˜æ®µå‰©ä½™å¤ªå°ï¼Œåˆ™ç›´æ¥æ˜¯è¿™æ•´ä¸ªå†…å­˜æ®µå°±å¥½ï¼Œé˜²æ­¢å°ç¢ç‰‡å‡ºç°ï¼‰
	  æ›´æ–°é€‰ä¸­çš„å†…å­˜çš„å‰©ä½™æƒ…å†µ
	  block[-1] = size + 1ï¼Œæ ‡è¯†å˜é‡ç”³è¯·çš„å ç”¨å†…å­˜å¤§å°ï¼Œåç»­ç”¨äºå†…å­˜å›æ”¶æ—¶ä½¿ç”¨
	  return block

// å›æ”¶å†…å­˜
deAlloc(object)
	segement = object - 1ï¼Œè¯¥æ®µå†…å­˜çš„åŸºåœ°å€
	segement.length = object[-1]ï¼Œå˜é‡å ç”¨å†…å­˜çš„å¤§å°
	å°†segementæ’å…¥freeList
```

```java
// ç”¨jackå®ç°
class Memory {
    static Array memory;
    static Array freeList;
    static Array NO_BLOCK;
    
    static int FL_LENGTH;  // freeListé•¿åº¦åœ¨segementä¸Šçš„indexï¼Œå€¼ä¸ºä¸º0
    static int FL_NEXT;    // freeListnextæŒ‡é’ˆåœ¨segementä¸Šçš„ndexï¼Œå€¼ä¸ºä¸º1
    static int ALLOC_SIZE; // åˆ†é…çš„blockçš„sizeï¼Œåœ¨blockä¸Šçš„indexï¼Œå€¼ä¸º-1
    
    /** åˆå§‹åŒ– */
    function void init() {
        let memory = 0;
        let freeList = 2048;    // heapåŸºåœ°å€
        let NO_BLOCK = 16384;   
        let FL_LENGTH = 0;
        let FL_NEXT = 1;
        let ALLOC_SIZE = -1;
        let freeList[FL_LENGTH] = 16384-2048;   //åˆå§‹åŒ–freeListï¼Œæ­¤æ—¶freeçš„å†…å­˜ä¸ºæ•´ä¸ªheap
        let freeList[FL_NEXT] = null;
        return;
    }

    /** è¿”å›åœ°å€ä¸ºaddressçš„å†…å­˜å•å…ƒçš„å†…å®¹ */
    function int peek(int address) {
        return memory[address];
    }

    /** æŠŠæ•°å€¼valueå­˜å‚¨åˆ°åœ°å€ä¸ºaddressçš„å†…å­˜å•å…ƒä¸­ */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** ä»å¯ç”¨å†…å­˜ä¸­å¯»æ‰¾ä¸€å—åˆé€‚çš„å†…å­˜å—ï¼Œè¿”å›å†…å­˜å—çš„åŸºåœ°å€ */
    function Array alloc(int size) {
        var Array prev_block;
        var Array found_block;
        
        let prev_block = Memory.best_fit(size);   // prev_block æ˜¯ç›®æ ‡ block çš„å‰ä¸€ä¸ªblock
        if( prev_block = NO_BLOCK ) {
            let found_block = null;               // æœªæ‰¾åˆ°
        }
        else {
            if( prev_block = null ) {
                let found_block = freeList;       // ç›®æ ‡ block æ˜¯ freeList çš„ç¬¬ä¸€ä¸ªèŠ‚ç‚¹
                let freeList = Memory.do_alloc(found_block, size); // åˆ†é…å†…å­˜ï¼Œå¹¶æ›´æ–°freeList
            }
            else {
                let found_block = prev_block[FL_NEXT];
                let prev_block[FL_NEXT] = Memory.do_alloc(found_block, size); // åˆ†é…å†…å­˜ï¼Œå¹¶æ›´æ–°freeList
            }
        }
        return found_block+1;
    }
    
    /** æœ€ä¼˜åŒ¹é…ç®—æ³• */
    function Array best_fit(int size) {
        var Array best_block;
        var Array prev_block;
        var Array cur_block;
        var int best_size;
        var int cur_size;
        
        let best_block = NO_BLOCK;
        let best_size = 16384 - 2048;
        let cur_block = freeList;
        let prev_block = null;
        
        // éå†freeListï¼Œæ‰¾åˆ°å¯ç”¨å†…å­˜å¤§äºsieï¼Œä¸”å…¶ä¸­æœ€å°çš„block
        while( ~(cur_block = null) ) {
            let cur_size = cur_block[FL_LENGTH]-1;
            if( ~(cur_size < size) & (cur_size < best_size) ) {
                let best_block = prev_block;
                let best_size = cur_size;
            }
            let prev_block = cur_block;
            let cur_block = cur_block[FL_NEXT];
        }
        
        return best_block;  // è¿”å›æœ€ä¼˜blockçš„å‰ä¸€ä¸ªblock
    }

    /** åˆ†é…å†…å­˜ï¼Œå¹¶å¡«å……block headerçš„å€¼ï¼ˆå€¼ä¸ºblockçš„é•¿åº¦ï¼‰*/
    function Array do_alloc(Array found_block, int size) {
        var Array next_block;
        var int block_size;
        
        if( found_block[FL_LENGTH] > (size+1+2) ) {          // blockå†…å­˜å¤§äºç”³è¯·å†…å†…å­˜ï¼Œåˆ†é…blockçš„ä¸€éƒ¨åˆ†
            let next_block = found_block + size+1;           // next_blockçš„ä½ç½®
            let next_block[FL_NEXT] = found_block[FL_NEXT];  // next_blocké‡ç½®blockä¿¡æ¯
            let next_block[FL_LENGTH] = found_block[FL_LENGTH] - (next_block - found_block);
            let found_block = found_block + 1;               // found_blocké‡ç½®blockä¿¡æ¯
            let found_block[ALLOC_SIZE] = size+1;            
        }
        else {                                               // æŠŠæ•´å—å†…å­˜éƒ½åˆ†é…ç»™ç”³è¯·è€…
            let next_block = found_block[FL_NEXT];
            let block_size = found_block[FL_LENGTH];
            let found_block = found_block + 1;               // found_blocké‡ç½®blockä¿¡æ¯
            let found_block[ALLOC_SIZE] = block_size;
        }
        
        return next_block;
    }

    /** é‡Šæ”¾å†…å­˜ */
    function void deAlloc(Array object) {
        var int alloc_size;
        var Array prev_block;
        var Array next_block;
        
        let alloc_size = object[ALLOC_SIZE];
        let object = object - 1;        
        let prev_block = Memory.find_prev_free(object);     // æ‰¾åˆ°prev_block
        
        if( prev_block = null ) {                           // objectæˆä¸ºæ–°çš„é¦–èŠ‚ç‚¹
            let object[FL_LENGTH] = alloc_size;
            let object[FL_NEXT] = freeList;
            let freeList = object;
            let prev_block = object;
        }
        else {
            if( (prev_block + prev_block[FL_LENGTH]) = object ) {
                // å¦‚æœ prev_block å’Œ object ç›¸è¿ï¼Œåˆ™åˆå¹¶ä¸¤ä¸ªblock
                let prev_block[FL_LENGTH] = prev_block[FL_LENGTH] + alloc_size;
            }
            else {
                // é“¾æ¥ä¸¤ä¸ªblock
                let object[FL_LENGTH] = alloc_size;
                let object[FL_NEXT] = prev_block[FL_NEXT];
                let prev_block[FL_NEXT] = object;
                let prev_block = object;
            }
        }
        
        if( (prev_block + prev_block[FL_LENGTH]) = prev_block[FL_NEXT] ) {
            // å°è¯•åˆå¹¶prev_blockå’Œnext_block
            let next_block = prev_block[FL_NEXT];
            let prev_block[FL_LENGTH] = prev_block[FL_LENGTH] + next_block[FL_LENGTH];
            let prev_block[FL_NEXT] = next_block[FL_NEXT];
        }
        return
    }    
    
    /** è¿”å›blockçš„prev_block */
    function Array find_prev_free(Array object) {
        var Array block;
        
        if( freeList > object ) {
            return null;
        }
        
        let block = freeList;
        while( ~(block[FL_NEXT] = null) & (block[FL_NEXT] < object) ) {
            let block = block[FL_NEXT];
        }
        return block;
    }
}
```

### I/O

è®¡ç®—æœºä¸€èˆ¬ä¼šè¿æ¥ä¸€äº›è¾“å…¥è¾“å‡ºè®¾å¤‡ï¼Œä¾‹å¦‚ï¼šæ˜¾ç¤ºå™¨ã€é¼ æ ‡ã€é”®ç›˜ã€ç¡¬ç›˜ã€ç½‘å¡ç­‰ï¼Œè¿™äº›I/Oè®¾å¤‡éƒ½æœ‰è‡ªå·±çš„ç‰¹æ€§ï¼Œåœ¨è¿™äº›è®¾å¤‡ä¸Šè¿›è¡Œè¯»å†™æ“ä½œä¹Ÿæ˜¯å¾ˆå¤æ‚çš„ï¼Œä½†å¯¹äºç¨‹åºå‘˜è€Œè¨€ï¼Œæˆ‘ä»¬å¯ä»¥é€šè¿‡æ“ä½œç³»ç»Ÿæ¥æ“ä½œå®ƒä»¬ï¼Œæ“ä½œç³»ç»Ÿä½¿ç”¨ä¸€ç»„é©±åŠ¨ç¨‹åºæ¥å¤„ç†å„ä¸ªI/Oè®¾å¤‡çš„æ“ä½œç»†èŠ‚ï¼Œç„¶åå‘ä¸Šæä¾›ä¸€ç»„å°è£…å¥½çš„APIæ¥å£ã€‚

I/Oè®¾å¤‡å¯¹äºCPUå’Œæ“ä½œç³»ç»Ÿè€Œè¨€ï¼Œå…¶å®æ˜¯ä¸€æ®µå†…å­˜å’Œä¸€ç»„BIOSé©±åŠ¨ç¨‹åºï¼ˆåœ¨Hackä¸­æ²¡æœ‰è¿™äº›é©±åŠ¨ç¨‹åºï¼Œéœ€è¦æˆ‘ä»¬è‡ªå·±å†™ï¼‰ï¼Œå½“æ“ä½œç³»ç»Ÿæƒ³è¦å’ŒI/Oè®¾å¤‡äº¤äº’æ—¶ï¼Œå°±é€šè¿‡è¿™äº›ç¨‹åºä»ç›¸åº”çš„å†…å­˜ä¸­è¯»å–æˆ–å†™å…¥æ•°æ®ã€‚åœ¨Hackä¸­ï¼Œæˆ‘ä»¬åªæœ‰ä¸¤ä¸ªI/Oè®¾å¤‡ï¼šæ˜¾ç¤ºå™¨å’Œé”®ç›˜ï¼Œ
æ˜¾ç¤ºå™¨çš„å†…å­˜æ˜ å°„æ˜¯ä»16384å¼€å§‹çš„è¿ç»­çš„ 8K åœ°å€ç©ºé—´ï¼ˆè¯¥æ˜¾ç¤ºå™¨çš„åˆ†è¾¨ç‡æ˜¯ï¼š512\*256ï¼‰ï¼›é”®ç›˜æ›´ç®€å•ï¼Œå®ƒåªæ˜ å°„24576è¿™ä¸€ä¸ªåœ°å€å•å…ƒçš„åœ°å€ç©ºé—´ï¼ˆå› ä¸ºè¶³ä»¥å®¹çº³æ‰€æœ‰è‹±æ–‡å­—ç¬¦å’Œç¬¦å·ï¼‰
<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcycyqes1sj30ww0p4jsi.jpg" width="700"></center>

### é”®ç›˜

å½“æˆ‘ä»¬æ•²å‡»é”®ç›˜æ—¶ï¼Œæ•²å‡»çš„å­—ç¬¦çš„saciiç ä¼šå†™å…¥åˆ°åœ°å€ä¸º24576çš„å†…å­˜ä¸­ã€‚æˆ‘ä»¬çš„é©±åŠ¨ç¨‹åºè¦åšçš„äº‹æƒ…å°±æ˜¯ï¼Œå®Œæˆä¸€ç»„ç‰¹å®šçš„APIï¼Œä»–ä»¬å¯ä»¥ä»24576å·åœ°å€ä¸­è¯»å–å­—ç¬¦ï¼ˆè¯»å–å•ä¸ªå­—ç¬¦æˆ–è€…å…ˆç»„åˆæˆå­—ç¬¦ä¸²ï¼‰ã€‚
<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcycyp9d74j315i0f6dgw.jpg" width="700"></center>

#### ç›‘æµ‹é”®ç›˜è¾“å…¥

ç¬¬ä¸€ä»¶äº‹éœ€è¦åšçš„æ˜¯ï¼Œç›‘æµ‹åˆ°é”®ç›˜çš„è¾“å…¥ã€‚è¯¥æ–¹æ³•çš„æ ¸å¿ƒå°±æ˜¯ä»é”®ç›˜çš„å†…å­˜æ˜ åƒä¸­è¯»å‡ºå½“å‰æŒ‰ä¸‹çš„å­—ç¬¦çš„asciiç ï¼Œå½“è¯»å–çš„å€¼ä¸ä¸º0çš„æ—¶å€™ï¼Œä»£è¡¨æœ‰å­—ç¬¦è¾“å…¥ã€‚

```java
// keyPress() ä¼ªä»£ç 
if æœ‰æŒ‰é”®æŒ‰ä¸‹
    return è¯¥é”®çš„saciiç 
else 
    return 0

// Jackå®ç°
function char keyPressed() {
    return keyboard[0];
}
```

#### è¯»å–å•ä¸ªå­—ç¬¦

è¯»å–å•ä¸ªå­—ç¬¦æ—¶æˆ‘ä»¬éœ€è¦é¢å¯¹ä¸¤ä¸ªé—®é¢˜ã€‚ç¬¬ä¸€ä¸ªï¼Œä»€ä¹ˆæƒ…å†µä¸‹æ‰å«ç”¨æˆ·è¾“å…¥äº†ä¸€ä¸ªå•ä¸ªå­—ç¬¦å‘¢ï¼Ÿè¿™é‡Œæˆ‘ä»¬å®šä¹‰ç”¨æˆ·æŒ‰ä¸‹ä¸€ä¸ªæŒ‰é”®ï¼Œç„¶åæ¾å¼€æŒ‰é”®ï¼Œè¿™è¿‡ç¨‹ä¼šè¾“å…¥ä¸€ä¸ªå­—ç¬¦ï¼ˆå³ä½¿é•¿æ—¶é—´æŒ‰ä½å†æ¾å¼€ä¹Ÿæ˜¯è¾“å…¥ä¸€ä¸ªï¼‰ï¼Œæ‰€ä»¥æˆ‘ä»¬éœ€è¦åšçš„äº‹æƒ…æ˜¯ç­‰å¾…ç”¨æˆ·è¾“å…¥ï¼Œç„¶åè®°å½•æ•°å€¼ï¼Œç„¶åç­‰å¾…ç”¨æˆ·æ¾å¼€ï¼Œå®Œæˆæœ¬æ¬¡å•ä¸ªå­—ç¬¦è¯»å–ã€‚ç¬¬äºŒä¸ªï¼Œç»™ç”¨æˆ·ä¸€ä¸ªå¯è§†åŒ–åé¦ˆï¼Œå½“ç”¨æˆ·è¾“å…¥ä¸€ä¸ªå­—ç¬¦åï¼Œéœ€è¦åœ¨å±å¹•ä¸Šå›æ˜¾å‡ºæ¥ï¼Œå¹¶ä¸”è¾…åŠ©ä»¥å…‰æ ‡æç¤ºï¼ˆè™½ç„¶è¿™ä¸ªæ“ä½œçœ‹ä¼¼ç†æ‰€å½“ç„¶ï¼Œä½†å…¶ä¸­ç»è¿‡çš„è¿‡ç¨‹å¹¶ä¸ç®€å•ï¼‰ã€‚

```java
// readChar() ä¼ªä»£ç 

while æ²¡æœ‰ä»»ä½•æŒ‰é”®æŒ‰ä¸‹
    ç­‰å¾…...ç›´åˆ°æœ‰æŒ‰é”®æŒ‰ä¸‹

c = å½“å‰æŒ‰ä¸‹çš„æŒ‰é”®å€¼

while å½“å‰æŒ‰é”®è¿˜å¤„äºæŒ‰ä¸‹çŠ¶æ€
    ç­‰å¾…...ç›´åˆ°æŒ‰é”®æ¾å¼€

print c // æŠŠå­—ç¬¦å›æ˜¾åˆ°å±å¹•ä¸Š
å°†å…‰æ ‡å³ç§»ä¸€æ ¼

return c
```

```java
// jackå®ç°
function char readChar() {
    var char key;
    while( Keyboard.keyPressed() = 0 ) {}
    let key = Keyboard.keyPressed();
    while( ~(Keyboard.keyPressed() = 0) ) {}
    do Output.printChar(key);
    return key;
}
```

#### è¯»å–ä¸€è¡Œå­—ç¬¦

ç„¶åæˆ‘ä»¬åœ¨ readChar() çš„åŸºç¡€ä¸Šæ‰©å±•å‡º readLine()ï¼ŒreadLine()éœ€è¦è§£å†³ä¸¤ä¸ªé—®é¢˜ã€‚ç¬¬ä¸€ä¸ªï¼ŒæŠŠè¯»å–çš„å•ä¸ªå­—ç¬¦å†™å…¥å­—ç¬¦ä¸²ç¼“å†²åŒºï¼Œç›´åˆ°è¾“å…¥æ¢è¡Œç¬¦æ—¶ç»“æŸã€‚ç¬¬äºŒä¸ªï¼Œå…è®¸ç”¨æˆ·åœ¨è¾“å…¥è¿‡ç¨‹ä¸­ä½¿ç”¨åˆ é™¤é”®åˆ é™¤

```java
// readLine() ä¼ªä»£ç 
s = ç©ºå­—ç¬¦ä¸²
repeat
    c = readChar()
    if c = æ¢è¡Œç¬¦
        print æ¢è¡Œç¬¦
        return s
    else if c = é€€æ ¼é”®
        s åˆ é™¤æœ€åä¸€ä¸ªå­—ç¬¦
        å…‰æ ‡å·¦ç§»ä¸€æ ¼
    else
        s = s.append(c)
```

```java
// Jackå®ç°
function String readLine(String message) {
    var String line;
    var char c;
    
    do Output.printString(message);
    
    let line = String.new(50);     // max length
    let c = Keyboard.readChar();
    while( ~(c = String.newLine()) ) {
        if( c = String.backSpace() ) {
            do line.eraseLastChar();
            do Output.backSpace();
        }
        else {
            do line.appendChar(c);
        }
        let c = Keyboard.readChar();
    }
    return line;
}   
```

å¯ä»¥çœ‹åˆ°ï¼Œé”®ç›˜è¾“å…¥çš„è®¾è®¡å®ç°è¿˜æ˜¯åŸºäºä¸€ç³»åˆ—çº§è”çš„åˆ†å±‚æŠ½è±¡ã€‚é«˜çº§ç¨‹åºreadLine()ä¾èµ–readChar()ï¼ŒreadChar()ä¾èµ–äºkeyPress()ï¼ŒkeyPress()ä¾èµ–äºç¡¬ä»¶ã€‚å¯è§åˆ†å±‚æŠ½è±¡çš„è®¾è®¡æ¨¡å¼è´¯ç©¿äº†æ•´ä¸ªè®¡ç®—æœºçš„å®è§‚æ¶æ„å’Œå¾®è§‚å®ç°ï¼Œå¯ä»¥è¯´æ˜¯è®¡ç®—æœºç§‘å­¦çš„ç²¾åæ€æƒ³ã€‚

### å›¾å½¢æ˜¾ç¤º

ç°ä»£è®¡ç®—æœºå¤§å¤šä½¿ç”¨ä½å›¾ï¼ˆbitmapï¼‰æˆ–ç§°å…‰æ …ï¼ˆrasterï¼‰çš„æŠ€æœ¯æ¥æ˜¾ç¤ºå›¾å½¢æˆ–æ–‡å­—ã€‚å±å¹•ç”±ä¸€ç»„äºŒç»´çš„å¹³é¢ç‚¹ç»„æˆï¼Œæ¯ä¸ªç‚¹æ˜¯ä¸€ä¸ªåƒç´ ï¼ˆpixelï¼‰ï¼Œæ¯ä¸ªåƒç´ ç‚¹å¯¹åº”åˆ°æ˜¾å­˜ä¸­çš„æŸä¸ªåœ°å€ï¼Œè®¡ç®—æœºè¦åšçš„å°±æ˜¯ç»™æ¯ä¸ªåƒç´ ç‚¹èµ‹ä¸Šä¸€ä¸ªé¢œè‰²çš„å€¼ï¼ŒHackåªæ”¯æŒé»‘å’Œç™½ï¼Œå› æ­¤åªå 1bitï¼Œå¦‚æœæ˜¾ç¤ºå™¨æ”¯æŒå½©è‰²æ˜¾ç¤ºï¼Œé‚£ä¹ˆæ¯ä¸ªåƒç´ å°±ä¼šå ç”¨æ›´å¤šbitçš„å†…å­˜ã€‚

Hackçš„å±å¹•åˆ†è¾¨ç‡æ˜¯512\*256ï¼Œå³å±å¹•ä¸Šæœ‰131072ä¸ªåƒç´ ç‚¹ã€‚åƒç´ ç‚¹å’Œæ˜¾å­˜çš„å¯¹åº”å…³ç³»å¦‚ä¸‹ï¼šç°å­˜ä¸­æ¯ä¸ªBitå¯¹åº”ä¸€ä¸ªåƒç´ ç‚¹ï¼ˆ1ä»£è¡¨é»‘è‰²ï¼Œ0ä»£è¡¨ç™½è‰²ï¼‰ï¼Œä¸€è¡Œ512ä¸ªåƒç´ éœ€è¦å¯¹åº” 32\*16bitï¼ˆä¸€ä¸ªwordï¼‰ï¼Œ32word/è¡Œ*256è¡Œ=8192wordï¼Œæ­£å¥½æ˜¯8Kåœ°å€ç©ºé—´ã€‚å› æ­¤å­˜åœ¨å¯¹åº”å…³ç³»ï¼šç¬¬rè¡Œç¬¬cåˆ—çš„åƒç´ ç‚¹ï¼Œå¯¹åº”åœ°å€ä¸º Screen[r\*32+c/16] çš„wordçš„ç¬¬ c%16 çš„bitã€‚

<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcz4oayk2sj30tq0dg74b.jpg" width="700"></center>

#### ç‚¹

äº†è§£äº†ä¸Šé¢åƒç´ ç‚¹å’Œå†…å­˜bitçš„å¯¹åº”å…³ç³»åï¼Œæˆ‘ä»¬å°±å¯ä»¥å†™å‡ºç»™å•ä¸€åƒç´ ç‚¹èµ‹å€¼çš„æ–¹æ³•äº†ã€‚

```java
/** ç”»åƒç´ ç‚¹ (x, y) */
function void drawPixel(int x, int y) {
    var int address;
    var int mask;

    let address = (y*32) + (x/16);       // å…ˆæ‰¾åˆ°ç¬¬å‡ ä¸ªword
    let mask = Math.two_to_the(x & 15);  // å†æ‰¾åˆ°è¯¥wordçš„ç¬¬å‡ ä¸ªbitï¼Œå¹¶è½¬æˆæ©ç çš„å½¢å¼ã€‚ä¾‹å¦‚ç¬¬4ä¸ªbitï¼Œå¯¹åº”0000000000000100

    if( cur_colour ) {
        let screen[address] = screen[address] | mask;
    }
    else {
        let screen[address] = screen[address] & ~mask;
    }

    return;
}

/** æ¸…é™¤å±å¹• */
function void clearScreen() {
    var int i;
    let i = 0;
    while( i < 8192 ) {
        let screen[i] = white;
    }
    return;
}

/** è®¾ç½®å°†è¦ä½¿ç”¨çš„color white = false, black = true. */
function void setColor(boolean b) {
    let cur_colour = b;
    return;
}
```

#### çº¿

ç‚¹å¾ˆå®¹æ˜“ç”»ï¼Œçº¿åˆ™å¤æ‚å¾ˆå¤šã€‚ç”±äºåƒç´ ç‚¹æ˜¯ä¸€ä¸ªä¸ªçŸ©å½¢çš„å°æ–¹å—ï¼Œæ‰€ä»¥é™¤äº†æ¨ªçº¿å’Œç«–çº¿ï¼Œæˆ‘ä»¬æ— æ³•ç”»å‡ºå®Œå…¨ç¬”ç›´çš„çº¿ï¼ˆæ–œçº¿éƒ½æ˜¯å¸¦é”¯é½¿æ¯›è¾¹çš„ï¼‰ï¼Œå½“ç„¶é€šè¿‡ç¼©å°åƒç´ ç‚¹çš„å¤§å°æˆ‘ä»¬å¯ä»¥æ— é™é€¼è¿‘ç¬”ç›´ï¼ˆå½“åƒç´ ç‚¹å°åˆ°äººçœ¼æ— æ³•åˆ†è¾¨æ—¶å°±å¯ä»¥äº†ï¼Œå³æ‰€è°“çš„è§†ç½‘è†œå±å¹•ï¼‰ã€‚
<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gcz5056jtyj30j00cyq2x.jpg" width="500"></center>

æˆ‘ä»¬åœ¨ç”»çº¿çš„æ—¶å€™ï¼Œåˆ†ä¸º3ç§åœºæ™¯ï¼šæ¨ªçº¿ã€ç«–çº¿å’Œæ–œçº¿ã€‚å…¶ä¸­æ¨ªçº¿å’Œç«–çº¿éƒ½æ¯”è¾ƒå®¹æ˜“å®ç°ï¼Œç”»æ–œçº¿åˆ™å¤æ‚ä¸€äº›ï¼Œéœ€è¦å€ŸåŠ©ç‰¹å®šçš„ç®—æ³•ã€‚

```java
/** ä» (x1, y1) åˆ° (x2, y2) ç”»ä¸€æ¡çº¿ */
function void drawLine(int x1, int y1, int x2, int y2) {
    var int dx, dy;
    var int temp;
    
    // ä¿è¯ x1 <= x2
    if( x1 > x2 ) {
        let temp = x1;
        let x1 = x2;
        let x2 = temp;
        let temp = y1;
        let y1 = y2;
        let y2 = temp;
    }

    let dx = x2 - x1;
    let dy = y2 - y1;
    
    if( dx = 0 ) {
        do Screen.drawVerticalLine( x1, y1, y2 );
    }
    else { if( dy = 0 ) {
        do Screen.drawHorizontalLine( x1, x2, y1 );
    }
    else {
        do Screen.drawDiagonalLine( x1, y1, x2, y2, dx, dy );
    }}
    
    return;
}
```

##### ç«–çº¿

ç”»ç«–çº¿è¾ƒå®¹æ˜“ï¼Œè°ƒç”¨ drawPixel() æ–¹æ³•ï¼Œä» y1 ç”»åˆ° y2 å³å¯ï¼ˆy1<=y2ï¼‰ã€‚

```java
function void drawVerticalLine( int x, int y1, int y2 ) {
    var int temp;
    
    // ä¿è¯ y1 <= y2
    if( y1 > y2 ) {
        let temp = y1;
        let y1 = y2;
        let y2 = temp;
    }
    
    while( ~(y1 > y2) ) {
        do Screen.drawPixel( x, y1 );
        let y1 = y1 + 1;
    }
    return;
}
```

##### æ¨ªçº¿

æ¨ªçº¿çš„å®ç°æ–¹å¼å¯ä»¥å’Œç«–çº¿ä¸€æ ·ï¼Œä¸€ä¸ªä¸ªåƒç´ çš„ç”»å®Œå³å¯ã€‚ä½†è¿™é‡Œæˆ‘ä»¬åšä¸€ä¸ªå°ä¼˜åŒ–ï¼Œä¸€æ¡æ¨ªçº¿ä¼šæ¨ªè·¨nä¸ªwordï¼Œé™¤äº†å¤´å’Œå°¾ï¼Œä¸­é—´çš„wordéƒ½æ˜¯è¿ç»­çš„ï¼Œæ‰€æœ‰æˆ‘ä»¬å¯ä»¥ä¸ºè¿™äº›wordä¸€æ¬¡æ€§èµ‹å€¼16ä¸ªbitï¼Œè¿™æ ·å¾ªç¯çš„æ¬¡æ•°ä¼šç¼©çŸ­16å€ã€‚

```java
function void drawHorizontalLine( int x1, int x2, int y ) {
    var int start_addr, end_addr;
    var int x1mod16, x2mod16;
    
    let x1mod16 = x1 & 15;               // çº¿åœ¨ä¸€ä¸ªwordå†…çš„åç§»é‡
    let x2mod16 = x2 & 15;               // çº¿åœ¨æœ€åä¸€ä¸ªwordå†…çš„åç§»é‡
    let start_addr = (y*32) + (x1/16);
    let end_addr = (y*32) + (x2/16) + (x2mod16=0);

    if( start_addr = end_addr ) {        // çº¿çš„é•¿åº¦åœ¨ä¸€ä¸ªwordå†…
        do Screen.draw_short_horizontal_line( x1, x2, y );
    }
    else { 
        if( ~(x1mod16 = 0) ) {           // çº¿åœ¨ç¬¬ä¸€ä¸ªWordå†…çš„ä¸€å°æ®µ
            let start_addr = start_addr + 1;
            do Screen.draw_short_horizontal_line( x1, x1+16-x1mod16, y );
        }
        if( ~(x2mod16 = 0) ) {           // çº¿åœ¨æœ€åä¸ªWordå†…çš„ä¸€å°æ®µ
            let end_addr = end_addr - 1;
            do Screen.draw_short_horizontal_line( x2-x2mod16, x2, y );
        }
        while( ~(start_addr > end_addr) ) {     // ä¸­é—´çš„è‹¥å¹²æ®µword
            let screen[start_addr] = cur_colour;
            let start_addr = start_addr + 1;
        }
    }
    
    return;
}

function void draw_short_horizontal_line( int x1, int x2, int y ) {
    while( ~(x1 > x2) ) {
        do Screen.drawPixel( x1, y );
        let x1 = x1 + 1;
    }

    return;
}
```

##### æ–œçº¿

æ–œçº¿çš„ç”»æ³•æ¯”ç›´çº¿å¤æ‚ï¼Œå› ä¸ºæˆ‘ä»¬åœ¨ç”»åƒç´ ç‚¹çš„æ—¶å€™åªèƒ½å‘ä¸Šã€ä¸‹ã€å·¦ã€å³å››ä¸ªæ–¹å‘ç”»ï¼Œè€Œä¸èƒ½æ–œç€ç”»ï¼Œæ‰€ä»¥æˆ‘ä»¬åªèƒ½å°½é‡è®©æˆ‘ä»¬çš„æ–œçº¿å’Œç›®æ ‡æ–œçº¿è¿‘ä¼¼ã€‚ä¸ºäº†å®Œæˆç”»æ–œçº¿ï¼Œæˆ‘ä»¬éœ€è¦å¼•å…¥æ–œç‡æ¥è¾…åŠ©ï¼šå¦‚ä¸‹å›¾æ‰€ç¤ºï¼Œæˆ‘ä»¬åœ¨ç”»æ–œçº¿æ—¶ï¼Œæ–°ç”»çš„åƒç´ ç‚¹çš„è¿åŠ¨ä½ç½®æ˜¯åƒè›‡ä¸€æ ·å·¦å³èœ¿èœ’å‰è¿›çš„ï¼Œæˆ‘ä»¬çš„ç®—æ³•çš„ç›®çš„å°±æ˜¯åˆ«è®©è¿™æ¡è›‡è·‘åï¼Œå½“å‰æ–œç‡å°äºç›®æ ‡æ–œç‡æ—¶ï¼Œæˆ‘ä»¬å°±å‘ä¸Šç”»åƒç´ ç‚¹ä»¥å¢å¤§æ–œç‡ï¼›å½“å‰æ–œç‡å¤§äºç›®æ ‡æ–œç‡æ—¶ï¼Œæˆ‘ä»¬å°±å‘å³ç”»åƒç´ ç‚¹ä»¥å‡å°æ–œç‡ã€‚

<center><img src="https://tva1.sinaimg.cn/large/00831rSTly1gczghjqsrsj314m0fqgmc.jpg" width="700"></center>

ç®—æ³•æ€æƒ³å¦‚ä¸Šè¿°ï¼Œæˆ‘ä»¬ä¸‹é¢è¦åšçš„å°±æ˜¯å®ç°å®ƒçš„æ™®éé€‚ç”¨ï¼ˆä¸åŒæ–¹å‘çš„æ–œçº¿ï¼‰çš„ä»£ç 

```java
function void drawDiagonalLine( int x1, int y1, int x2, int y2, int dx, int dy ) {
    var int a, b;
    var int adyMinusbdx;
    var int y_incr;

    let a = 0;
    let b = 0;
    let adyMinusbdx = 0;
    
    if( dy < 0 ) {
        let y_incr = -1;
    }
    else {
        let y_incr = 1;
    }

    while( ~(a > dx) & (((y_incr = 1) & ~(b > dy)) | ((y_incr = -1) & ~(b < dy))) ) {
        do Screen.drawPixel( x1+a, y1+b );
        if( adyMinusbdx < 0 ) {
            let a = a + 1;
            let adyMinusbdx = adyMinusbdx + (dy*y_incr);
        }
        else {
            let b = b + y_incr;
            let adyMinusbdx = adyMinusbdx - dx;
        }
    }
    return;
}
```


#### çŸ©å½¢

#### åœ†

### æ–‡å­—æ˜¾ç¤º

### System

æœ¬ç¯‡å®Œæ•´å®ç°æ”¾åœ¨äº†:[github](https://github.com/guosainpu/Nand2Tetris)
